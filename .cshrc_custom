source ~/.cshrc_alias

########################################
# CUSTOM TERMINAL SETTINGS
########################################

if ($term == "xterm" || $term == "vt100" \
    || $term == "vt102" || $term !~ "con*") then
        # bind keypad keys for console, vt100, vt102, xterm
        bindkey "\e[1~" beginning-of-line  # Home
        bindkey "\e[7~" beginning-of-line  # Home rxvt
        bindkey "\e[2~" overwrite-mode     # Ins
        bindkey "\e[3~" delete-char        # Delete
        bindkey "\e[4~" end-of-line        # End
        bindkey "\e[8~" end-of-line        # End rxvt
endif

# Colors!
set     red="%{\033[1;31m%}"
set   green="%{\033[0;32m%}"
set  yellow="%{\033[1;33m%}"
set    blue="%{\033[1;34m%}"
set magenta="%{\033[1;35m%}"
set    cyan="%{\033[1;36m%}"
set   white="%{\033[0;37m%}"
set     end="%{\033[0m%}"

set prompt="${green}%n${blue}@%m ${white}%~ ${green}%%${end} "
set prompt="${blue}[${green}%n${blue}@%m ${white}%~ ${blue}]${end} "

# Clean up after ourselves...
unset red green yellow blue magenta cyan yellow white end

########################################
# CUSTOM ENV VARIABLES
########################################

setenv TERM xterm-256color
setenv TN_VECTOR_NO_DB 1
setenv P4DIFF vimdiff
setenv EDITOR /home/utils/vim-8.0-lua/bin/vim
setenv DISPLAY sc-xterm-06:2000

########################################
# CUSTOM AUTO COMPLETE
########################################

set autolist = ambiguous
set complete = enhance

complete p4 p/1/"(add branch branches change changes client clients counter counters \
delete depot depots describe diff diff2 edit filelog files fix fixes \
fstat group groups have help info integrate integrated job jobs jobspec \
label labels labelsync lock obliterate opened passwd print protect \
rename reopen resolve resolved revert review reviews set submit sync \
triggers unlock user users verify where shelve unshelve)"/ \
'n@-*c@`p4 opened |& awk \{print\ \$4,\$5\} | sort -u | /home/utils/grep-2.18/bin/grep -o "default\|[0-9]*"`@' \
'n@{change,describe}@`p4 opened |& awk \{print\ \$4,\$5\} | sort -u | /home/utils/grep-2.18/bin/grep -o "[0-9]*"`@'

complete make p/1/"(SwarmReview SwarmReviewUpdate Review JETSubmit)"/ \
'c@CL=@`p4 opened |& awk \{print\ \$4,\$5\} | sort -u | /home/utils/grep-2.18/bin/grep -o "[0-9]*"`@' \
'n/Review/(CL=)//' \
'n/SwarmReview/(CL=)//' \
'n/SwarmReviewUpdate/(CL=)//' \
'n/JETSubmit/(CL=)//'

complete editp4 'n@-*c@`p4 opened |& awk \{print\ \$4,\$5\} | sort -u | /home/utils/grep-2.18/bin/grep -o "default\|[0-9]*"`@'

complete setenv p/1/e/
complete printenv p/1/e/
complete unsetenv p/1/e/

